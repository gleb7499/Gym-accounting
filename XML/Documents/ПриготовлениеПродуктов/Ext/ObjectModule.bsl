
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Продукты.Записывать = Истина;
	
	// группируем данные таблицы -> сохраняем в ВТ -> вытаскиваем остатки из регистра накоплений -> соединяем таблицы ->
	// -> получаем итоговую таблицу с готовыми вычисленными полями
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриготовлениеПродукцииИнгредиенты.Продукт КАК Продукт,
	|	СУММА(ПриготовлениеПродукцииИнгредиенты.Количество) КАК Количество
	|ПОМЕСТИТЬ втИнгредиентыДокумента
	|ИЗ
	|	Документ.ПриготовлениеПродуктов.Ингредиенты КАК ПриготовлениеПродукцииИнгредиенты
	|ГДЕ
	|	ПриготовлениеПродукцииИнгредиенты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриготовлениеПродукцииИнгредиенты.Продукт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИнгредиентыДокумента.Продукт.Представление КАК Продукт,
	|	втИнгредиентыДокумента.Количество КАК Количество,
	|	ЕСТЬNULL(ПродуктыОстатки.СуммаОстаток / ПродуктыОстатки.КоличествоОстаток * втИнгредиентыДокумента.Количество, 0) КАК Сумма,
	|	ВЫБОР
	|		КОГДА втИнгредиентыДокумента.Количество <= ПродуктыОстатки.КоличествоОстаток
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Достаточно,
	|	ПродуктыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	втИнгредиентыДокумента КАК втИнгредиентыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продукты.Остатки(
	|				&МоментВремени,
	|				Продукт В
	|					(ВЫБРАТЬ
	|						втИнгредиентыДокумента.Продукт КАК Продукт
	|					ИЗ
	|						втИнгредиентыДокумента КАК втИнгредиентыДокумента)) КАК ПродуктыОстатки
	|		ПО втИнгредиентыДокумента.Продукт = ПродуктыОстатки.Продукт";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// списываем продукты, использованные в качестве ингредиентов
	Пока Выборка.Следующий() Цикл
		// проверка достатка
		Если Не Выборка.Достаточно Тогда
			Сообщить("Недостаточно продукта " + Выборка.ПродуктПредставление + "! Осталось " + ?(ЗначениеЗаполнено(Выборка.КоличествоОстаток), Выборка.КоличествоОстаток, 0));
			Отказ = Истина;
			Возврат;
		КонецЕсли;		
		
		Движение = Движения.Продукты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период 	 = Дата;
		Движение.Продукт 	 = Выборка.Продукт;
		Движение.Количество  = Выборка.Количество;
		Движение.Сумма       = Выборка.Сумма;
	КонецЦикла;
	
	// записываем приготовленный продукт в регистр накоплений
	Движение = Движения.Продукты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период      = Дата;
	Движение.Продукт     = Продукт;
	Движение.Количество  = Количество;
	Движение.Сумма       = Цена;
	
КонецПроцедуры

