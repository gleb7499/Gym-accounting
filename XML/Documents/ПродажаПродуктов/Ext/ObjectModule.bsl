Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Проверка заполненности таблицы
	Если РеализованныеПродукты.Количество() = 0 Тогда
		Сообщить("Добавьте данные в таблицу!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, Режим)	
	
	// Флаг движения Истина
	Движения.Продукты.Записывать = Истина;
	
	// Запрос к текущей таблице и регистру остатков. Формирование остатков продуктов, введенных в таблицу текущего документы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажаПродуктовРеализованныеПродукты.Продукт КАК Продукт,
	|	СУММА(ПродажаПродуктовРеализованныеПродукты.Количество) КАК Количество
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПродажаПродуктов.РеализованныеПродукты КАК ПродажаПродуктовРеализованныеПродукты
	|ГДЕ
	|	ПродажаПродуктовРеализованныеПродукты.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажаПродуктовРеализованныеПродукты.Продукт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеДокумента.Продукт КАК Продукт,
	|	втДанныеДокумента.Количество КАК Количество,
	|	ЕСТЬNULL(ПродуктыОстатки.СуммаОстаток / ПродуктыОстатки.КоличествоОстаток * втДанныеДокумента.Количество, 0) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПродуктыОстатки.КоличествоОстаток < втДанныеДокумента.Количество
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Достаточно,
	|	втДанныеДокумента.Продукт.Представление КАК ПродуктПредставление
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продукты.Остатки(
	|				,
	|				Продукт В
	|					(ВЫБРАТЬ
	|						втДанныеДокумента.Продукт КАК Продукт
	|					ИЗ
	|						втДанныеДокумента КАК втДанныеДокумента)) КАК ПродуктыОстатки
	|		ПО втДанныеДокумента.Продукт = ПродуктыОстатки.Продукт";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Проверяем достаток продуктов и записываем в регистр накоплений в режиме расход
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.Достаточно Тогда
			Сообщить("Не хватает продукта " + Выборка.ПродуктПредставление);
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
		
		Движение = Движения.Продукты.Добавить();
		Движение.ВидДвижения  = ВидДвиженияНакопления.Расход;
		Движение.Период 	  = Дата;
		Движение.Продукт 	  = Выборка.Продукт;
		Движение.Количество   = Выборка.Количество;
		Движение.Сумма 		  = Выборка.Сумма;
	КонецЦикла;
	
КонецПроцедуры
